# Maintainer: Tobias Powalowski <tpowa@archlinux.org>

pkgname=qemu
pkgver=1.5.1
pkgrel=2
pkgdesc="A generic and open source processor emulator which achieves a good emulation speed by using dynamic translation."
arch=('i686' 'x86_64')
groups=('modified')
license=('GPL2' 'LGPL2.1')
url="http://wiki.qemu.org/Index.html"
makedepends=('texi2html' 'perl' 'python2')
depends=('pixman' 'libjpeg' 'libpng' 'sdl' 'alsa-lib' 'nss' 'glib2'
         'gnutls>=2.4.1' 'vde2' 'util-linux' 'curl' 'libsasl'
         'libgl' 'libpulse' 'seabios' 'libcap-ng' 'libaio' 'libseccomp'
         'libiscsi' 'spice>=0.12.2')
backup=('etc/qemu/target-x86_64.conf')
install=qemu.install
source=(http://wiki.qemu.org/download/${pkgname}-${pkgver}.tar.bz2
        65-kvm.rules)
replaces=('qemu-kvm')
options=(!strip)

build ()
{
  cd "${srcdir}/${pkgname}-${pkgver}"
  # gtk gui breaks keymappings at the moment
  ./configure --prefix=/usr \
              --sysconfdir=/etc \
              --libexecdir=/usr/lib/qemu \
              --python=/usr/bin/python2 \
              --target-list='i386-softmmu i386-linux-user x86_64-softmmu x86_64-linux-user arm-softmmu arm-linux-user' \
              --audio-drv-list='pa alsa sdl' \
              --enable-mixemu \
              --enable-linux-aio \
              --enable-seccomp \
              --enable-kvm \
              --enable-spice \
              --enable-smartcard-nss \
              --enable-docs \
              --disable-gtk \
              --disable-bluez \
              --disable-xen
  make
}

package() {
  cd "${srcdir}/${pkgname}-${pkgver}"

  make DESTDIR="${pkgdir}" install

  # provided by seabios
  rm "${pkgdir}/usr/share/qemu/bios.bin"

  # udev rule
  install -D -m644 "${srcdir}/65-kvm.rules" "${pkgdir}/usr/lib/udev/rules.d/65-kvm.rules"

  # bridge_helper needs suid
  # https://bugs.archlinux.org/task/32565
  chmod u+s "${pkgdir}/usr/lib/qemu/qemu-bridge-helper"

  # add sample config
  echo "allow br0" > ${pkgdir}/etc/qemu/bridge.conf.sample

  # strip scripts directory
  find "${pkgdir}/usr/src/linux-${_kernver}/scripts"  -type f -perm -u+w 2>/dev/null | while read binary ; do
    if [[ "$(file -b --mime-type "$binary")" == "application/x-executable" ]]; then
      # Binaries
      strip $STRIP_BINARIES "$binary"
    fi
  done
}

md5sums=('b56e73bdcfdb214d5c68e13111aca96f'
         '9d6de26867a05c306157e3d3c612b28a')
