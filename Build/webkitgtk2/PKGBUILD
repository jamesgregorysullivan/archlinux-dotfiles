# Maintainer: Andreas Radke <andyrtr@archlinux.org>

pkgname=webkitgtk2
pkgdesc="GTK+ Web content engine library for GTK+ 2.0"
pkgver=1.10.2
pkgrel=5
arch=('i686' 'x86_64')
groups=('modified')
url="http://webkitgtk.org/"
license=('custom')
depends=('libxt' 'libxslt' 'sqlite' 'libsoup' 'enchant' 'libgl' 'gtk2' 'gstreamer0.10-base')
makedepends=('gperf' 'gobject-introspection' 'python2' 'mesa' 'ruby' 'gtk-doc')
conflicts=('libwebkit')
provides=("libwebkit=${pkgver}")
replaces=('libwebkit')
options=('!libtool' '!emptydirs')
source=(http://webkitgtk.org/releases/webkitgtk-$pkgver.tar.xz)
sha256sums=('19234f8e4edfaefe91ed06471a2aa8c71d26dd55d33787eede1dcfc9a3a96d9c')

build() {
  cd "$srcdir/webkitgtk-$pkgver"
  mkdir -p build-gtk2

  cd build-gtk2
  PYTHON=/usr/bin/python2 ../configure --prefix=/usr \
    --enable-introspection \
    --disable-silent-rules \
    --libexecdir=/usr/lib/webkitgtk2 \
    --with-gstreamer=0.10 \
    --with-gtk=2.0 \
    --disable-webkit2 \
    --disable-geolocation
  make all stamp-po
}

package() {
  cd "$srcdir/webkitgtk-$pkgver/build-gtk2"
  make -j1 DESTDIR="$pkgdir" install
  install -Dm644 ../Source/WebKit/LICENSE "$pkgdir/usr/share/licenses/${pkgname}/LICENSE"
}
