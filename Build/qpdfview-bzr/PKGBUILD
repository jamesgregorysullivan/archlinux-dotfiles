# Maintainer: Adam Reichold <adamreichold@myopera.com>
# Contributor: Stefan Husmann <stefan-husmann@t-online.de>
pkgname=qpdfview-bzr
pkgver=1262
pkgrel=1
pkgdesc='A tabbed PDF viewer using the poppler library. (development version)'
arch=('i686' 'x86_64')
groups=('modified')
url='https://launchpad.net/qpdfview'
license=('GPL2')
depends=('file' 'libcups' 'poppler-qt4')
optdepends=('libspectre: for PostScript support' 'djvulibre: for DjVu support')
makedepends=('bzr')
conflicts=('qpdfview')
install='qpdfview.install'
source=('qpdfview::bzr+http://bazaar.launchpad.net/~adamreichold/qpdfview/trunk/')
md5sums=('SKIP')

pkgver() {
  cd "$srcdir/qpdfview"
  
  bzr revno
}

build() {
  cd "$srcdir/qpdfview"
  
  if ! pkg-config --exists libspectre; then
    local config="$config without_ps"
  fi
  
  if ! pkg-config --exists ddjvuapi; then
    local config="$config without_djvu"
  fi

  lrelease-qt4 qpdfview.pro qpdfview.pro
  qmake-qt4 "CONFIG+=$config" qpdfview.pro
  make
}

package() {
  cd "$srcdir/qpdfview"

  make "INSTALL_ROOT=$pkgdir" install
}
